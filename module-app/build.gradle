plugins {
    id 'com.google.cloud.tools.jib' version '3.3.2'
}

bootJar {
    enabled = true
    mainClass = 'com.example.booklibrary.BookLibraryApplication'

    archiveBaseName.set(rootProject.getName())
    archiveVersion.set(rootProject.getVersion().toString())
    archiveFileName.set("${archiveBaseName.get()}-${archiveVersion.get()}.jar")
}

//--------------------------------------------Integration Tests--------------------------------------

sourceSets {
    testIntegration {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/testIntegration/java')
        resources.srcDir file('src/testIntegration/resources')
    }
}

tasks.register('testIntegration', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    mustRunAfter test

    useJUnitPlatform()
}

configurations {
    testIntegrationImplementation.extendsFrom(implementation, testImplementation)
}

check.dependsOn testIntegration


//--------------------------------------------Jib Plugin--------------------------------------

jib {
    from {
        image = "amazoncorretto:20-alpine-jdk"
    }
    to {
        image = project.hasProperty('imageTag')
                ? "${rootProject.getName()}:${project.property('imageTag')}"
                : "${rootProject.getName()}:${rootProject.getVersion()}"
    }
    container {
        mainClass = "com.example.booklibrary.BookLibraryApplication"
    }
}

if (project.hasProperty('env')) {
    jib.to.image = project.property('env') == 'dev'
            ? "${rootProject.getName()}:latest"
            : "${rootProject.getName()}:${rootProject.getVersion().toString()}"
}

tasks.register('deployAndRun') {
    dependsOn 'clean', 'build', 'jibDockerBuild'

    doLast {
        exec {
            commandLine 'docker-compose', 'up'
        }
    }
}


//--------------------------------------------Jacoco Aggregated Report--------------------------------------

def aggregatedProjects = [':module-book', ':module-user-security', ':module-purchase', ':module-recommendation', ':module-report', ':module-common']

tasks.register('jacocoGeneralReport', JacocoReport) {

    dependsOn = aggregatedProjects.collect { "$it:jacocoTestReport" }

    sourceDirectories.setFrom(files(aggregatedProjects.collect { project(it).sourceSets.main.allSource.srcDirs }))
    classDirectories.setFrom(files(aggregatedProjects.collect { project(it).sourceSets.main.output }))
    executionData.from(aggregatedProjects.collect { project(it).jacocoTestReport.executionData })

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}


//--------------------------------------------Dependencies--------------------------------------

dependencies {
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"

    implementation project(':module-book')
    implementation project(':module-user-security')
    implementation project(':module-purchase')
    implementation project(':module-recommendation')
    implementation project(':module-report')
    implementation project(':module-common')

    testIntegrationImplementation "org.testcontainers:junit-jupiter"
    testIntegrationImplementation "org.testcontainers:mysql"
    testIntegrationImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
}